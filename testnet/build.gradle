group 'com.lumenaut'
version '1.0-SNAPSHOT'

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Plugins

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Config
buildscript {
    // Extra properties (valid for all projects if defined here)
    ext {
        junitPlatformVersion = '1.0.2'
        junitJupiterVersion = '5.0.2'
        stellarSdkVersion = '0.3.3'
        jacksonVersion = '2.9.3'
        okHttpVersion = '3.12.0'
        shadowJarVersion = '4.0.3'
    }

    // Repositories for build scripts
    repositories {
        // Libraries from extras
        flatDir dirs: "$rootDir/libs"

        // Maven
        mavenCentral()
        jcenter()

        // Sonatype repositories
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    // Dependencies for build scripts
    dependencies {
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadowJarVersion"
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Compiler flags
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        // Warn about deprecations
        options.compilerArgs << "-Xlint:deprecation"

        // Warn about unchecked casts
        options.compilerArgs << "-Xlint:unchecked"
    }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Repositories

repositories {
    // Jars
    flatDir dirs: "$rootDir/libs"

    // Maven
    mavenCentral()

    // Sonatype repositories
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Dependencies

dependencies {
    // Unit tests
    testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"

    // To avoid compiler warnings about @API annotations in JUnit 5
    testCompileOnly('org.apiguardian:apiguardian-api:1.0.0')

    // JSON parser
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion"

    // HTTP Client
    compile "com.squareup.okhttp3:okhttp:$okHttpVersion"

    // Stellar SDK
    compile "stellar-sdk:stellar-sdk:$stellarSdkVersion"
}

// Release config

jar {
    manifest {
        attributes 'Description': 'Testnet Tools'
        attributes 'Main-Class': 'Main'
    }
}

shadowJar {
    configurations = [project.configurations.compile]
}


////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Execution task

task run(dependsOn: classes, type: JavaExec) {
    main = "com.lumenaut.poolmanager.Main"
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Gradle wrapper

task wrapper(type: Wrapper) {
    gradleVersion = '4.10'
}